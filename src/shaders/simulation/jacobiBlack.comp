#version 430

#include "includes.comp"
#include "layout_size.comp"

layout(rgba32f, binding = 0) uniform image2D pressure_WRITE;
layout(binding = 1) uniform sampler2D pressure_READ;
layout(binding = 2) uniform sampler2D divergence;

// The divergence and pressure is packed in an half-size texture.
// This black pass updates the bottom left and top right points (black points)
// using neighbooring red points.
// For example result(22) = p(12) + p(32) + p(23) + (21) - div(22) 
//                        = p(0, 1).g + p(1, 1).a + p(1, 1).g + p(1, 0).a - div(1, 1).g;

// 03 ------ 13        23 ------ 33
// |          |        |          |
// |  (0, 1)  |        |  (1, 1)  |
// |          |        |          |
// 02 ------ 12        22 ------ 32
//                            
//                            
// 01 ------ 11        21 ------ 31
// |          |        |          |
// |  (0, 0)  |        |  (1, 0)  |
// |          |        |          |
// 00 ------ 10        20 ------ 30

void main()
{
  ivec2 tSize = TEXTURE_SIZE(pressure_READ);
  ivec2 pixelCoords = ivec2(gl_GlobalInvocationID.xy);
  const ivec2 dx = ivec2(1, 0); 
  const ivec2 dy = ivec2(0, 1);

  vec4 dC = texelFetch(divergence, pixelCoords, 0);

  vec4 pL = texelFetchOffset(pressure_READ, pixelCoords, 0, - dx);
  vec4 pR = texelFetchOffset(pressure_READ, pixelCoords, 0,   dx);
  vec4 pB = texelFetchOffset(pressure_READ, pixelCoords, 0, - dy);
  vec4 pT = texelFetchOffset(pressure_READ, pixelCoords, 0,   dy);

  vec4 pC = texelFetch(pressure_READ, pixelCoords, 0);
  if(pixelCoords.x == 0) pL.y = pC.x;
  if(pixelCoords.y == 0) pB.w = pC.x;
  if(pixelCoords.x == tSize.x - 1) pR.w = pC.z;
  if(pixelCoords.y == tSize.y - 1) pT.y = pC.z;

  float r = 0.25 * (pL.y + pC.y + pB.w + pC.w - dC.x);
  float b = 0.25 * (pC.w + pR.w + pC.y + pT.y - dC.z);

  imageStore(pressure_WRITE, pixelCoords, vec4(r, pC.y, b, pC.w));
}
